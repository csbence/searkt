group 'unh-ai'
version '1.0-SNAPSHOT'

apply plugin: 'kotlin'
apply plugin: 'application'

//applicationDefaultJvmArgs = [
//        "-server", "-XX:+AggressiveOpts", "-ea",
//        "-Xms100g",
//        "-Xmx200g"
//]

if (project.hasProperty("mainClass")) {
    mainClassName = mainClass
} else {
    mainClassName = "edu.unh.cs.ai.realtimesearch.RealTimeSearchKt"
}

buildscript {
    ext.kotlin_version = '1.1.0-beta-38'
    repositories {
        mavenCentral()
        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    mavenCentral()
    maven {
        url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'org.apache.commons:commons-math3:3.0'
    compile 'commons-cli:commons-cli:1.3.1'

    compile 'org.springframework:spring-web:4.2.5.RELEASE'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.4'
    compile('com.fasterxml.jackson.module:jackson-module-kotlin:2.8.4') {
        exclude module: 'org.jetbrains.kotlin:kotlin-reflect:1.0.0'
    }

    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'org.slf4j:slf4j-api:1.7.11'
    compile 'org.codehaus.groovy:groovy-all:2.4.5'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

sourceSets {
    main.java.srcDirs += file('src/main/kotlin')
    test.java.srcDirs += file('src/test/kotlin')
}

task plot(type: Exec) {
    workingDir 'scripts'
    commandLine 'python3', 'metronome_plot.py'
}

run {
    if (project.hasProperty("appArgs")) {
        args = Eval.me(appArgs)
    }
}

jar {
    manifest {
        attributes "Main-Class": "$mainClassName"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task clientJar(type: Jar) {
    manifest {
        //noinspection GroovyAssignabilityCheck
        attributes 'Main-Class': 'edu.unh.cs.ai.realtimesearch.experiment.network.RealTimeSearchClientApplicationKt'
    }
    baseName = project.name + '-client'


    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}